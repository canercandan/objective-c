/**
 * @file koocModule.inc.cws
 * @brief koocModule functions
 * @note @ref develop @ref contrib @ref tools
 */

declare function	koocModuleInit(sModule : value);
declare function	koocModuleContent(sModule : value);

/**
 * @fn function	koocModuleInit(sModule : value)
 * @brief init the tree'information to insert module
 * @param sModule module's name
 */
function	koocModuleInit(sModule : value)
{
  insert this.kooc.modules[sModule];
  insert this.kooc.modules[sModule].block;
}

/**
 * @fn function	koocModuleCheck(sModule : value)
 * @brief check if the declaration is valid
 * @param sModule module's name
 */
function	koocModuleCheck(sModule : value)
{
  localref theNode = this.kooc.modules[sModule].block#back;

  if (theNode.type != "__VARIABLE__" &&
      theNode.type != "__PROTOTYPE__" &&
      theNode.type != "__TYPE__")
    {
      traceLine(getInputFilename() + ":" + countInputLines() + ": "
                + "error: unexpected identifier");
      exit(-1);
    }
}

/**
 * @fn function	koocModuleMangle(sModule : value)
 * @brief call mangle only if we must mangle it
 * @param sModule module's name
 */
function	koocModuleMangle(sModule : value)
{
  localref theNode = this.kooc.modules[sModule].block#back;

  if (theNode.type == "__VARIABLE__" ||
      theNode.type == "__PROTOTYPE__")
    {
      koocMangle(sModule);
    }
}

/**
 * @fn koocModuleContent(sModule : value)
 * @brief fill the tree and generate C code and header
 * @param sModule module's name
 */
function	koocModuleContent(sModule : value)
{
  local		modH = "km_" + replaceString('.', '_', sModule.toLowerString()) + ".h";
  local		modC = "km_" + replaceString('.', '_', sModule.toLowerString()) + ".c";

  insert this.kooc.modules[sModule].nameH = modH;
  insert this.kooc.modules[sModule].nameC = modC;
  generate("mod.h.cwt", this.kooc.modules[sModule], this.kooc.path_import + modH);
  generate("mod.c.cwt", this.kooc.modules[sModule], this.kooc.path_import + modC);
  {%>#include "@modH@"@"\n"@<%}
}
