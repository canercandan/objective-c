/**
 * @file kooc.cws
 * @brief Kooc generator file
 * @note @ref develop @ref contrib @ref tools
 */

#include "koocTree.inc.cws"

declare function	mainFunc(file : value);
declare function	testFile(file : value);
declare function	initTree(file : value);
declare function	loopFile();
declare function	setPathFiles(file : value);
declare function	setPathImport(file : value);

loopFile();

/**
 * @fn loopFile()
 * @brief get all files from ARGS
 */
function	loopFile()
{
  local		i = 0;

  while (_ARGS[$i$] != "")
    {
      testFile(_ARGS[$i$]);
      initTree(_ARGS[$i$]);
      mainFunc(_ARGS[$i$]);
      removeAllElements(project);
      i = $i + 1$;
    }
  exit(0);
}

/**
 * @fn mainFunc(file : value)
 * @brief It's the main function of kooc
 * @param file filename
 */
function	mainFunc(file : value)
{
  traceLine("[KOOC] Processing: [" + getEnv("pathKc")
	    + "] to [" + getEnv("pathC") + "].");
  translate("./kooc.cwp", project, getEnv("pathKc"),
	    getEnv("pathC"));
  saveProject(getEnv("pathTree"));
  traceLine("[KOOC] PrettyPrinter...");
  indentFile(getEnv("pathC"), "C++");
  traceLine("[KOOC] Done.");
}

/**
 * @fn testFile(file : value)
 * @brief test if the file in argument exist else exit
 * @param file filename
 * @return exit 42 if error
 */
function	testFile(file : value)
{
  if (existFile(file) == false)
    {
      traceLine("Unable to find file [" + file + "]");
      exit(42);
    }
}

/**
 * @fn initTree(file : value)
 * @brief init the tree'functions
 * @param file filename
 */
function	initTree(file : value)
{
  treeInit();
  treeKooc();
  setPathFiles(file);
  setPathImport(file);
}

function	setPathFiles(file : value)
{
  local		sRoot = getWorkingPath();
  local		sExtension = rightString(file, 3);

  putEnv("pathKc", file);
  putEnv("pathTree", sRoot + replaceString(sExtension, ".ktree", file));
  putEnv("pathC", sRoot + replaceString(sExtension, ".c", file));
}

function	setPathImport(file : value)
{
  local		size = lengthString(file);
  local		pos = $size - findLastString(file, '/')$;

  pos = $pos - 1$;
  if ($pos != size$)
    putEnv("path_import", rsubString(file, pos));
  else
    putEnv("path_import", "./");
}
