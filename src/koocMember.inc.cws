/**
 * @file koocMember.inc.cws
 * @brief koocMember functions
 * @note @ref develop @ref contrib @ref tools
 */

declare function	koocMemberDeclaration(theClass : node);
declare function	koocMemberDeclarationIfVariable(theClass : node, theDecl : node);
declare function	koocMemberDeclarationIfFunction(sClass : value, theDecl : node);
declare function	koocMemberInsertReference(theClass : node, sClass : value, theDecl : node);
declare function	koocMemberCreateNew(theDecl : node);
declare function	koocMemberAddSelf(sClass : value, theNode : node);

function	koocMemberDeclaration(theClass : node)
{
  local		sClass = koocMangleSpacer('.');
  localref	theDecl = this.block#back;

  koocMemberDeclarationIfVariable(theClass, theDecl);
  koocMemberCreateNew(theDecl);
  koocMemberDeclarationIfFunction(sClass, theDecl);
  koocMemberInsertReference(theClass, sClass, theDecl);
}

function	koocMemberDeclarationIfVariable(theClass : node, theDecl : node)
{
  if (theDecl.type == "__VARIABLE__")
    {
      pushItem theClass.ctype.list;
      setall theClass.ctype.list#back = theDecl;
      removeElement(this.block.variables, theDecl.name);
    }
}

function	koocMemberDeclarationIfFunction(sClass : value, theDecl : node)
{
  if (theDecl.type != "__VARIABLE__")
    {
      koocMemberAddSelf(sClass, theDecl);
      treeInsertFileOut(getEnv("path_import")
			+ this.kooc.classes[sClass].nameH,
			theDecl);
    }
}

function	koocMemberInsertReference(theClass : node, sClass : value, theDecl : node)
{
  pushItem this.kooc.classes[sClass];
  if (theDecl.type == "__VARIABLE__")
    ref this.kooc.classes[sClass]#back = theClass.ctype.list#back;
  else
    ref this.kooc.classes[sClass]#back = theDecl;
    
}

function	koocMemberCreateNew(theDecl : node)
{
  if (theDecl.oname == "init")
    {
      pushItem this.block;
      localref theSrc = this.block#back;
      setall theSrc = theDecl;
      theSrc.name = replaceString("init", "new", theSrc.name);
      theSrc.oname = replaceString("init", "new", theSrc.oname);
    }
}

function	koocMemberAddSelf(sClass : value, theNode : node)
{
  local		name;
  local		self;
  local		ctype;

  insert ctype.pctx;
  ref ctype.pctx = this.gl_pctx;

  // Create du ctype
  cnormCreateCtypeNode(ctype);

  // Self is a pointer
  cnormFillCtypeNode(ctype, "__POINTER__", "*");

  insert ctype.type = "__COMPOSED__";
  insert ctype.identifier = sClass;

  // Create declaration
  cnormCreateDeclarationNode(self, "__VARIABLE__");

  name = "self";
  cnormFillDeclarationNode(self, "__NAME__", name);
  cnormFillDeclarationNode(self, "__CTYPE__", ctype);

  invertArray(theNode.list);

  pushItem theNode.list;
  setall theNode.list#back = self;

  invertArray(theNode.list);
}
