/**
 * @file koocMangle.inc.cws
 * @brief Kooc Mangle file
 * @note @ref develop @ref contrib @ref tools
 * @todo fonction mangle generique (i do it soon !!!)
 * @bug Temp mangling
 */

declare function	koocMangle(theMod : value);
declare function	koocMangleImplementation(theMod : value);
declare function	koocMangleType(theNode : node, name : node);
declare function	koocMangleArgs(theNode : node, name : node);

/**
 * @fn mangle(theMod : value)
 * @brief mangle variable
 * @param theMod variable's name
 */
function	koocMangle(theMod : value)
{
  local		pkgs = "";
  local		name = "_ZN";
  localref	theNode = this.kooc.modules[theMod].block#back;

  if (existVariable(theNode.ctype.infoType["static"]))
    return;
  if (this.kooc.modules[theMod].package != "")
    {
      cutString(this.kooc.modules[theMod].package, '.', pkgs);
      foreach pkg in pkgs
	name += lengthString(pkg) + pkg;
    }
  name += lengthString(theMod) + theMod;
  name += lengthString(theNode.name) + theNode.name;
  name += "E";
  koocMangleType(theNode, name);
  koocMangleArgs(theNode, name);
  insert theNode.oname = theNode.name;
  insert theNode.name = name;
  if (theNode.type == "__VARIABLE__")
    {
      ref this.kooc.modules[theMod].block.variables[theNode.name] =
	this.kooc.modules[theMod].block.variables[theNode.oname];
      removeElement(this.kooc.modules[theMod].block.variables, theNode.oname);
    }
  else if (theNode.type == "__PROTOTYPE__")
    {
      ref this.kooc.modules[theMod].block.prototypes[theNode.name] =
	this.kooc.modules[theMod].block.prototypes[theNode.oname];
      removeElement(this.kooc.modules[theMod].block.prototypes, theNode.oname);
    }
}

function	koocMangleImplementation(theMod : value)
{
  local		name = "_ZN" + lengthString(theMod) + theMod;
  localref	theNode = this.kooc.implementations[theMod].block#back;

  if (existVariable(theNode.ctype.infoType["static"]))
    return;
  name += lengthString(theNode.name) + theNode.name;
  name += "E";
  koocMangleType(theNode, name);
  koocMangleArgs(theNode, name);
  insert theNode.oname = theNode.name;
  insert theNode.name = name;
  if (theNode.type == "__FUNCTION__")
    {
      ref this.kooc.implementations[theMod].block.functions[theNode.name] =
	this.kooc.implementations[theMod].block.functions[theNode.oname];
      removeElement(this.kooc.implementations[theMod].block.functions, theNode.oname);
    }
}

function	koocMangleType(theNode : node, name : node)
{
  if (theNode.ctype.type == "__PRIMARY__")
    name += theNode.ctype.identifier.charAt(0);
  else
    name += lengthString(theNode.ctype.identifier)
      + theNode.ctype.identifier;
}

function	koocMangleArgs(theNode : node, name : node)
{
  if (existVariable(theNode.list))
    {
      foreach item in theNode.list
	if (item.ctype.type == "__PRIMARY__")
	  name += item.ctype.identifier.charAt(0);
	else
	  name += lengthString(item.ctype.identifier)
	    + item.ctype.identifier;
    }
}
