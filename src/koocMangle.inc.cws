/**
 * @file koocMangle.inc.cws
 * @brief Kooc Mangle file
 * @note @ref develop @ref contrib @ref tools
 * @todo fonction mangle generique (i do it soon !!!)
 * @bug Temp mangling
 */

declare function	koocMangle(theType : node);
declare function	koocMangleType(theNode : node, name : node);
declare function	koocMangleArgs(theNode : node, name : node);
declare function	koocMangleInit();
declare function	koocMangleSpacer(spacer : value);
declare function	koocManglePushBlock(sName : value);
declare function	koocManglePopBlock();
declare function	koocManglePopAllBlock();

function	koocMangle(theType : node)
{
  local		sMangle = "_ZN";

  if (this.kooc.mangle.empty() ||
      existVariable(theType.ctype.infoType["static"]))
    return (theType.name);
  foreach var in this.kooc.mangle
  {
    sMangle += lengthString(var);
    sMangle += var;
  }
  sMangle += lengthString(theType.name);
  sMangle += theType.name;
  sMangle += "E";
  koocMangleType(theType, sMangle);
  koocMangleArgs(theType, sMangle);
  return (sMangle);
}

function	koocMangleType(theNode : node, name : node)
{
  if (theNode.ctype.type == "__PRIMARY__")
    name += theNode.ctype.identifier.charAt(0);
  else
    name += lengthString(theNode.ctype.identifier)
      + theNode.ctype.identifier;
}

function	koocMangleArgs(theNode : node, name : node)
{
  if (existVariable(theNode.list))
    {
      foreach item in theNode.list
	if (item.ctype.type == "__PRIMARY__")
	  name += item.ctype.identifier.charAt(0);
	else
	  name += lengthString(item.ctype.identifier)
	    + item.ctype.identifier;
    }
}

function	koocMangleInit()
{
  insert this.kooc.mangle;
}

function	koocMangleSpacer(spacer : value)
{
  local		sResult = "";

  if (this.kooc.mangle.empty())
    return ("");
  foreach var in this.kooc.mangle
  {
    sResult += var;
    if (!last(var))
      sResult += spacer;
  }
  return (sResult);
}

function	koocManglePushBlock(sName : value)
{
  local		block;

  if (findString(sName, '.') != -1)
    {
      cutString(sName, '.', block);
      foreach var in block
	pushItem this.kooc.mangle = var;
    }
  else
    pushItem this.kooc.mangle = sName;
}

function	koocManglePopBlock()
{
  removeVariable(this.kooc.mangle#back);
}

function	koocManglePopAllBlock()
{
  removeAllElements(this.kooc.mangle);
}
