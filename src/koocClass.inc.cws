/**
 * @file koocClass.inc.cws
 * @brief koocClass functions
 * @note @ref develop @ref contrib @ref tools
 */

declare function	koocClassDeclaration(sClass : value);
declare function	koocClassInit(sClass : value, sExtend : value);
declare function	koocClassFill(sClass : value, sExtend : value);
declare function	koocClassInitVtable(sClass : value, sExtend : value);
declare function	koocClassExtend(sClass : value, sExtend : value, theNode : node);
declare function	koocClassErr(theNode : node);
declare function	koocClassAdd(sClass : value);
declare function	koocClassPrint(sClass : value);
declare function	koocClassExtendErr(sExtend : value);


function	koocClassDeclaration(sClass : value)
{
  localref	theBlock = this.block#back;
//   local		isSelf = (theBlock.ctype.list#front.identifier == sClass &&
// 			  existVariable(theBlock.ctype.list#front.pointer) &&
// 			  theBlock.ctype.list#front.pointer.getArraySize() == 1);

//   if (!isSelf)
//     {
//       insert theBlock.ctype.infoType;
//       insert theBlock.ctype.infoType["static"]
// 	= "storageClassSpecifier";
//     }
  if (this.block#back.type == "__VARIABLE__")
    treeInsertFileOut(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH + "_variable",
		      theBlock);
  else
    treeInsertFileOut(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH,
		      theBlock);
}

/**
 * @fn function	koocClassInit(sClass : value)
 * @brief init the tree'information to insert module
 * @param sClass module's name
 */
function	koocClassInit(sClass : value, sExtend : value)
{
  local		modH = "kc_" + replaceString('.', '_', sClass) + ".h";
  local		modC = "kc_" + replaceString('.', '_', sClass) + ".c";

  insert this.kooc.classes[sClass];
  insert this.kooc.implementations[sClass];
  insert this.kooc.classes[sClass].nameH = modH;
  insert this.kooc.classes[sClass].nameC = modC;
  insert this.kooc.classes[sClass].parent = sExtend;
}

function	koocClassInitVtable(sClass : value, sExtend : value)
{
  local decl;
  local name;
  local ctype;

  insert ctype.pctx;
  ref ctype.pctx = this.gl_pctx;

  insert ctype.type = "__COMPOSED__";
  insert ctype.identifier = "_kc_implementation_" + sClass;
  insert ctype.specifier = "struct";
  insert ctype.list;

  if (sExtend)
  {
    local pname = "_kc_implementation_" + sExtend;
    setall ctype.list = this.block.structs[pname].ctype.list;
  }

  insert decl.type = "__TYPE__";
  insert decl.etype = "declaration";
  cnormFillDeclarationNode(decl, "__CTYPE__", ctype);

  pushItem this.block;
  setall this.block#back = decl;

  treeInsertFileOut(getEnv("path_import") + this.kooc.classes[sClass].nameH,
		    this.block#back);

  ref this.block.structs[ctype.identifier] = this.block#back;
}

function	koocClassFillVtable(sClass : value, sExtend : value)
{
  local decl;
  local name;
  local ctype;

  insert ctype.pctx;
  ref ctype.pctx = this.gl_pctx;

  insert ctype.type = "__COMPOSED__";
  insert ctype.identifier = "_kc_implementation_" + sClass;
  insert ctype.specifier = "struct";

  cnormCreateDeclarationNode(decl, "__VARIABLE__");

  name = "_kc_vtable_" + sClass;
  cnormFillDeclarationNode(decl, "__NAME__", name);
  cnormFillDeclarationNode(decl, "__CTYPE__", ctype);

  insert decl.init;
  insert decl.init.block;
  insert decl.init.etype = "init";

  if (sExtend)
  {
    local pname = "_kc_vtable_" + sExtend;
    setall decl.init.block = this.block.variables[pname].init.block;
  }

  pushItem this.block;
  setall this.block#back = decl;

  treeInsertFileOut(getEnv("path_import") + this.kooc.classes[sClass].nameC,
  		    this.block#back);

  ref this.block.variables[name] = this.block#back;
}

function	koocClassFill(sClass : value, sExtend : value)
{
  local		ctype;

  insert ctype.pctx;
  ref ctype.pctx = this.gl_pctx;
  insert ctype.type = "__COMPOSED__";
  insert ctype.identifier = "_kc_interface_" + sClass;
  insert ctype.infoType;
  insert ctype.infoType["typedef"] = "storageClassSpecifier";
  insert ctype.specifier = "struct";
  insert ctype.list;

  // vtable pointer
  local vt_decl;
  local vt_ctype;

  ref vt_ctype.pctx = this.gl_pctx;

  cnormFillCtypeNode(vt_ctype, "__POINTER__", "*");
  insert vt_ctype.identifier = "void";

  cnormCreateDeclarationNode(vt_decl, "__VARIABLE__");
  cnormFillDeclarationNode(vt_decl, "__CTYPE__", vt_ctype);
  insert vt_decl.name = "vt";

  pushItem ctype.list;
  setall ctype.list#back = vt_decl;

  pushItem this.block;
  treeInsertFileOut(getEnv("path_import")
		    + this.kooc.classes[sClass].nameH,
		    this.block#back);
  insert this.block#back.name = sClass;
  insert this.block#back.type = "__TYPE__";
  insert this.block#back.etype = "declaration";
  setall this.block#back.ctype = ctype;
  insert this.block.types[sClass];
  ref this.block.types[sClass] = this.block#back;
  if (sExtend)
    koocClassExtend(sClass, sExtend, this.block#back.ctype.list);
}

function	koocClassExtend(sClass : value, sExtend : value, theNode : node)
{
  if (!existVariable(this.block.types[sExtend]))
    {
      traceLine("error: unknown class " + sExtend);
      exit(-1);
    }

  setall theNode = this.block.types[sExtend].ctype.list;
}

function	koocClassErr(theNode : node)
{
  if (theNode.type == "__VARIABLE__" &&
      theNode.type == "__PROTOTYPE__")
    {
      traceLine("\"" + theNode.name + "\" is a incorrect declaration in @class");
      exit(-1);
    }
}

function	koocClassAdd(sClass : value)
{
  localref	theDecl = this.block#back;

  pushItem this.kooc.classes[sClass];
  ref this.kooc.classes[sClass]#back = this.block#back;
  if (theDecl.type == "__VARIABLE__")
    treeInsertFileOut(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH + "_variable",
		      theDecl);
  else
    treeInsertFileOut(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH,
		      theDecl);
}

function	koocClassPrint(sClass : value)
{
  koocImportIfndef(this.kooc.classes[sClass].nameH);
  {@@prettyPrinterToH(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH + "_variable")@@}
  {@@prettyPrinterToC(getEnv("path_import")
		      + this.kooc.classes[sClass].nameH)@@}
  koocImportEndif(this.kooc.classes[sClass].nameH);
}

function	koocClassExtendErr(sExtend : value)
{
  if (existClass(sExtend) == 0)
    {
      traceLine("Class " + sExtend + " doesn't exist");
      exit(-1);
    }
}
